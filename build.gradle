buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.12.2'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    classpath 'me.tatarka:gradle-retrolambda:2.2.3'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'retrolambda'
apply plugin: 'hugo'

repositories {
  jcenter()
  maven {
    url = 'https://oss.jfrog.org/oss-snapshot-local/'
  }
}

dependencies {
  compile 'com.android.support:support-v4:20.0.0'

  compile 'com.inkapplications:android-preferences:1.1.0'

  compile 'com.jakewharton.timber:timber:2.2.2'
  compile 'com.jakewharton:butterknife:5.1.2'
  debugCompile 'com.jakewharton.madge:madge:1.1.1'
  debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'

  compile 'com.squareup.retrofit:retrofit:1.6.1'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
  compile 'com.squareup.okhttp:okhttp:2.0.0'
  compile 'com.squareup.okio:okio:1.0.1'
  compile 'com.squareup.dagger:dagger:1.2.2'
  provided 'com.squareup.dagger:dagger-compiler:1.2.2'

  compile 'com.github.frankiesardo:icepick:2.3.6'
  provided 'com.github.frankiesardo:icepick-processor:2.3.6'

  compile 'com.netflix.rxjava:rxjava-core:0.20.3'
  compile 'com.netflix.rxjava:rxjava-android:0.20.3'

  compile 'com.andrewreitz:shillelagh:0.2.1-SNAPSHOT'
  provided 'com.andrewreitz:shillelagh-processor:0.2.1-SNAPSHOT'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 20
  buildToolsVersion "19.1.0"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 20

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  signingConfigs {
    release {
      storeFile file(storeFilePath)
      storePassword keystorePassword
      keyAlias storeKeyAlias
      keyPassword aliasKeyPassword
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }

    release {
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}
