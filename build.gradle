buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.0.0'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'hugo'

repositories {
  jcenter()
  maven {
    url = 'https://oss.jfrog.org/oss-snapshot-local/'
  }
}

dependencies {
  compile 'com.android.support:support-v4:21.0.2'
  compile 'com.android.support:support-annotations:21.0.2'

  compile 'com.inkapplications:android-preferences:1.1.0'
  compile 'com.inkapplications:prism:0.1.0-SNAPSHOT'

  compile 'com.jakewharton.timber:timber:2.5.0'
  compile 'com.jakewharton:butterknife:6.0.0'
  debugCompile 'com.jakewharton.madge:madge:1.1.1'
  debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'

  compile 'com.squareup.retrofit:retrofit:1.8.0'
  compile 'com.squareup.picasso:picasso:2.4.0'

  compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
  compile 'com.squareup.okhttp:okhttp:2.1.0'
  compile 'com.squareup.okio:okio:1.0.1'

  compile 'com.squareup.flow:flow:0.8'
  compile 'com.squareup.mortar:mortar:0.16'

  compile 'com.squareup.dagger:dagger:1.2.2'
  provided 'com.squareup.dagger:dagger-compiler:1.2.2'

  compile 'com.squareup:otto:1.3.5'

  compile 'com.github.frankiesardo:icepick:2.3.6'
  provided 'com.github.frankiesardo:icepick-processor:2.3.6'

  compile 'io.reactivex:rxandroid:0.23.0'

  compile 'joda-time:joda-time:2.5'

  compile 'com.google.guava:guava:18.0'

  compile 'com.andrewreitz:shillelagh:0.5.0'
  provided 'com.andrewreitz:shillelagh-processor:0.5.0'

  compile 'com.andrewreitz.velcro:velcro-infinitescroll:1.0.1'
  compile 'com.andrewreitz.velcro:velcro-betterviewanimator:1.0.1'
  compile 'com.andrewreitz.velcro:velcro-bindableadapter:1.0.1'
  compile 'com.andrewreitz.velcro:velcro-rx:1.0.1'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.1"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 21

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  signingConfigs {
    release {
      storeFile file(storeFilePath)
      storePassword keystorePassword
      keyAlias storeKeyAlias
      keyPassword aliasKeyPassword
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }

    release {
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}
